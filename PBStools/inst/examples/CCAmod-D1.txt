model{

# WinBUGS model for catch-curve analysis
# Dirichlet distribution - Case 1 (survival)

# Data (in a separate file):
# k, B, -- the control vector
# (A is not used directly, although ai[g]=A)
# g -- number of age groups
# ai[1:g] -- cut points (as in equation 5, with ai[g]=A)
# y[1:g] -- grouped proportions (observed)

# Parameters:
# Z, n

# *************************************************

# Priors for the parameters (Table 7)
Z ~ dunif(0,1)
n ~ dunif(10,1000)

# *************************************************

# Model proportions by age (Table 2)
# Includes intermediate quantities S[k:B]
# Gives pa[k:B] and ppi[1:g]
# (name ppi avoids potential conflict with pi=3.14159)

# Calculation for k:B
# S[k:B]
for (a in k:B) {
  S[a] <- exp(-Z*(a-k)) }
# pa[k:B]
xsum <- sum(S[k:B])
for (a in k:B) { pa[a] <- S[a]/xsum }

# Grouped proportions (equation 7)
ppi[1] <- sum(pa[k:ai[1]])
for (i in 2:(g-1)) {
  ppi[i] <- sum(pa[(ai[i-1]+1):ai[i]])}
ppi[g] <- sum(pa[(ai[g-1]+1):B])

# *************************************************

# Dirichlet distribution
for (i in 1:g) {alp[i] <- n * ppi[i]}  # standard Dirichlet alphas
y[1:g] ~ ddirch(alp[1:g])

}
